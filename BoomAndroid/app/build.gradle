buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def getExpiryDate() {
    def now = Calendar.getInstance();
    now.add(Calendar.MONTH, 1)
    return now.getTime().format('dd-MM-yyyy')
}

android {
    def EXPIRY_DATE = "EXPIRY_DATE"
    def STRING = { k -> "\"${k}\"" }


    compileSdkVersion 27
    buildToolsVersion '26.0.2'
    flavorDimensions "default"


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.globaldelight.boom"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode buildNumber as Integer
        versionName "1.0.0"
        multiDexEnabled true

        resValue 'string', 'version', "v"+versionName

        buildConfigField "boolean",'ENABLE_LOG',                'true'
        buildConfigField("String",  EXPIRY_DATE,                STRING(getExpiryDate()) )
        buildConfigField "String", 'MIXPANEL_SENDER_ID',        STRING("862807752058")
        buildConfigField "String", 'FLURRY_API_KEY',            STRING("MGGVKKG7JYXGC3N89B4V")
        buildConfigField "String", 'MIXPANEL_PROJECT_TOKEN',    STRING("598c4011c2d961ef3b7f248459fa30c7")
        buildConfigField "String", 'AUDIO_CONFIG_URL',          STRING("http://devboom2.globaldelight.net/audioconfig.php")
        buildConfigField "String", 'FEEDBACK_URL',              STRING("http://devboom2.globaldelight.net/feedback.php?os=android")

        ndk {
            abiFilters = ['armeabi-v7a']
        }
    }

    buildTypes {

        debug {
            buildConfigField "String",  'FLURRY_API_KEY',            STRING("")
            buildConfigField "boolean", 'ENABLE_LOG',                'true'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {

        demo {
            buildConfigField "String",  'MIXPANEL_PROJECT_TOKEN',    "null"
        }

        store {
            buildConfigField "String", 'GOOGLE_VIDEO_AD_ID',        STRING("ca-app-pub-3940256099942544/5224354917")
            buildConfigField "String", 'GOOGLE_NATIVE_AD_ID',       STRING("ca-app-pub-3940256099942544/2247696110")
        }

        production {
            buildConfigField "boolean",'ENABLE_LOG',                'false'
            buildConfigField "String",  EXPIRY_DATE,                 "null"
            buildConfigField "String",  'FLURRY_API_KEY',            STRING("XRPWTN4PDGJJQX6NNJPN")
            buildConfigField "String",  'MIXPANEL_PROJECT_TOKEN',    STRING("79a795acec1fbcc3d9d28f792c51fc24")
            buildConfigField "String",  'GOOGLE_VIDEO_AD_ID',        STRING("ca-app-pub-2462021843550393/4811995067")
            buildConfigField "String",  'GOOGLE_NATIVE_AD_ID',       STRING("ca-app-pub-2462021843550393/9799761460")
        }

        b2b {
            applicationIdSuffix ".b2b"

            buildConfigField "boolean", 'ENABLE_LOG',                'true'
            buildConfigField "String",   EXPIRY_DATE,                 "null"
            buildConfigField "String",  'FLURRY_API_KEY',            STRING("G6P5DBXKYF5C3NH4S752")
            buildConfigField "String",  'MIXPANEL_PROJECT_TOKEN',    "null"
            buildConfigField "String",  'FEEDBACK_URL',               STRING("https://boom3dapp.com/feedback/android/deviceid/%s/language/%s/")
        }

        whitelabel {
            resConfigs "en"

            buildConfigField "String",  'FLURRY_API_KEY',            "null"
            buildConfigField "String",  'MIXPANEL_PROJECT_TOKEN',    "null"
            buildConfigField "String",  'FEEDBACK_URL',               STRING("https://boom3dapp.com/feedback/android/deviceid/%s/language/%s/")
        }
    }

    sourceSets {
        production {
            setRoot 'src/store'
        }
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
    maven { url 'https://maven.fabric.io/public' }
}


dependencies {
    implementation 'com.afollestad.material-dialogs:core:0.9.0.2'

    implementation('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true
    }

    implementation('com.google.api-client:google-api-client-android:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.apis:google-api-services-drive:v3-rev52-1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }


    implementation(name: 'library-core', ext: 'aar') {
        force = true
    }
    implementation(name: 'extension-flac-release', ext: 'aar')
    implementation(name: 'boom-extension-release', ext: 'aar')

    implementation project(path: ':boom-engine')

    implementation 'com.dropbox.core:dropbox-core-sdk:3.0.5'
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'

    implementation 'com.android.support:support-v4:27.0.0'
    implementation 'com.android.support:cardview-v7:27.0.0'
    implementation 'com.android.support:design:27.0.0'
    implementation 'com.android.support:recyclerview-v7:27.0.0'
    implementation 'com.android.support:appcompat-v7:27.0.0'
    implementation 'com.android.support:palette-v7:27.0.0'

    implementation 'com.afollestad.material-dialogs:core:0.9.0.2'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.flurry.android:analytics:8.2.0@aar'
    implementation 'com.appsflyer:af-android-sdk:4.7.2@aar'
    implementation 'com.google.android.gms:play-services-base:11.4.2'
    implementation 'com.google.android.gms:play-services-auth:11.4.2'
    implementation 'com.google.firebase:firebase-appindexing:11.4.2'
    implementation 'com.google.android.gms:play-services-drive:11.4.2'
    implementation 'pub.devrel:easypermissions:0.2.1'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.google.android.gms:play-services-gcm:11.4.2'
    implementation "com.mixpanel.android:mixpanel-android:5.1.4"

    // Dependencies for store
    storeImplementation 'com.google.android.gms:play-services-ads:11.4.2'
    storeImplementation('com.facebook.android:audience-network-sdk:4.+' ) {
        exclude group: 'com.google.android.exoplayer'
    }
    storeImplementation('com.facebook.android:facebook-android-sdk:4.5.0') {
        exclude module: 'bolts-android'
    }

    // Dependencies for production
    productionImplementation 'com.google.android.gms:play-services-ads:11.4.2'
    productionImplementation('com.facebook.android:audience-network-sdk:4.+' ) {
        exclude group: 'com.google.android.exoplayer'
    }
    productionImplementation('com.facebook.android:facebook-android-sdk:4.5.0') {
        exclude module: 'bolts-android'
    }
}

// ADD THIS AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'
